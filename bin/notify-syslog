#!/usr/bin/awk -f

# shell quoting function 
function quote(s,
    SINGLE, QSINGLE, i, X, n, ret)  # locals
{
    if (s == "") return "\"\"";

    SINGLE = "\x27"  # single quote
    QSINGLE = "\"\x27\""
    n = split(s, X, SINGLE)

    ret = SINGLE X[1] SINGLE
    for (i = 2; i <= n; i++)
        ret = ret QSINGLE SINGLE X[i] SINGLE

    return ret
}

# parse log line line
{ 
    line=$0;
    # save fields
    month=$1;
    day=$2; 
    time=$3; 
    host=$4;; 
    subsys=$5;
    sub(":","",subsys); 
    # clear fields to recover rest of message
    $1=""; $2=""; $3=""; $4=""; $5="";
    msg=$0;
    sub(/^[ \t]*/,"",msg);
    gsub("<","\\&lt;",msg);
    gsub(">","\\&gt;",msg);
    urgency="low";
    if(msg ~/[wW][aA][rR][nN][iI][nN][gG]/) urgency="normal";
    if(msg ~ /[eE][rR][rR][oO][rR]/) urgency="critical";
    device=0;
    if(subsys ~ "kernel" && msg ~ /^[[:graph:]]*:/)
    {
        device=msg;
        sub(/:.*/,"",device);
    }
    pid=0;
    # extract a pid
    if(match(subsys,/\[[[:digit:]]+\]/)) {
        pid=substr(subsys,RSTART+1,RLENGTH-2);
    }
}

# resend repeated messages ignoring the count
subsys=="syslogd" && msg~/last message repeated/ {
    if(command) system(command);
    # if(command) print(command);
    next;
}

# filter notifications
subsys ~ /wpa_supplicant/ && msg ~ /Group rekeying completed/ {ignore=1}
device ~ /hdacc[0-9]/ && msg ~ /Unexpected unsolicited response with tag/ {ignore=1}
subsys ~ /battmond/{urgency="critical"}

# send notification if ignore=0
!ignore {
    subsystem=subsys
    command="notify-send -a syslog -u " urgency " " quote(subsystem) " " quote(msg);
    # print(line);
    # print(month,day,time,host,subsys,device,msg);
    # print(command);
    # print(pid,device);
    system(command);
}
# clear ignore flag after each message
{if(ignore) command=0; ignore=0}
